1 REM SIEVE OF ERATOSTHENES
2 REM GET SECONDS TILL START OF PROGRAM
4 S=INT(TI/60)
5 REM *********
6 REM VAR NUMBERS OF PRIMES FOUND
7 NOPRIM=0
8 REM *********
9 REM INIT ARRAY AND FILL
10 N=2851
20 DIM A(N)
30 FOR I=2 TO N
40 A(N)=0
50 NEXT I
58 REM *********
59 REM LOOK FOR PRIME NUMBERS
60 FOR I=2 TO SQR(N)
70 IF A(I)=0 THEN GOSUB 1000
80 NEXT I
90 FOR I=SQR(N)+1 TO N
100 IF A(I)=0 THEN GOSUB 2000
110 NEXT I
117 REM *********
118 REM PRINT EVERYTHING, GET AND CALC
119 REM TIME
120 PRINT "[inverted heart]"
125 PRINT "FOUND";NOPRIM;"PRIME NUMBERS"
126 PRINT
130 E=INT(TI/60)
140 EL=E-S
150 PRINT "SECONDS AT START:",S
160 PRINT "SECONDS AT END:",E
170 PRINT "SECONDS ELAPSED:",EL                               
173 EL=EL/60
175 PRINT "MINUTES ELAPSED:",INT(EL)
180 END
998 REM *********
999 REM GOSUB ROUTINES
1000 REM WRITE PRIME NUMBER AND DELETE
1001 REM FROM ARRAY
1002 REM REMOVE REM BEFORE PRINT IF YOU
1003 REM WANT TO SEE ALL PRIME NUMBERS
1005 NOPRIM=NOPRIM+1
1010 REM PRINT I
1020 FOR B=I*I TO N STEP I
1030 A(B)=1
1040 NEXT B
1050 RETURN
2000 REM WRITE PRIME NUMBERS ABOVE SQR
2001 REM N. SO ALL IN ARRAY WITH VALUE 0
2002 REM REMOVE REM BEFORE PRINT IF YOU
2003 REM WANT TO SEE ALL PRIME NUMBERS
2005 NOPRIM=NOPRIM+1
2010 REM PRINT I
2020 RETURN
